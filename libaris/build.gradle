plugins {
    id 'java-library'
    id 'antlr'
}

apply plugin: 'antlr'

dependencies {

    compile project(':service')

    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'org.apache.logging.log4j:log4j-api:2.10.0'
    compile 'org.apache.logging.log4j:log4j-core:2.10.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'

    implementation 'org.antlr:antlr4-runtime:4.7.1'
    antlr 'org.antlr:antlr4:4.7.1'

    generateGrammarSource {
        arguments += ['-visitor'] // https://stackoverflow.com/questions/29971097/how-to-create-ast-with-antlr4
        arguments += ['-DcontextSuperClass=org.antlr.v4.runtime.RuleContextWithAltNum'] // https://stackoverflow.com/questions/39538191/how-to-receive-altnumber-in-antlr
    }

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

build { dependsOn "rust-build" }
test {
    dependsOn "rust-build"
    dependsOn "rust-test"
 }

task "rust-build" {
    dependsOn "compileJava"
    doFirst {
        exec {
            workingDir './src/main/rust'
            commandLine 'cargo', 'build', '--release'
        }
        copy {
            into './build/resources/main'
            from './src/main/rust/target/release/liblibaris_rs.so'
        }
        copy {
            into '../proof-client/build/resources/main'
            from './src/main/rust/target/release/liblibaris_rs.so'
        }
    }
}

task "rust-test" {
    doFirst {
        exec {
            workingDir './src/main/rust'
            commandLine 'cargo', 'test', '--release', '--', '--nocapture'
        }
    }
}

task "get-runtime-classpath" {
    doFirst {
        def classpath = ''
        sourceSets.main.runtimeClasspath.each {
            classpath += ':' + it
        }
        println classpath
    }
}
