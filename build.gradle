plugins {
    id 'java'
    id 'java-library'
    id 'antlr'
}

allprojects {
    group = 'edu.rpi.aris'
    version = new File('libaris/src/main/resources/edu/rpi/aris/VERSION').text.trim()

    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'antlr'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.8

    jar.doFirst {
        manifest {
            attributes("Implementation-Title": "Aris",
                    "Implementation-Version": version,
                    'Class-Path': configurations.runtime.collect { it.name }.join(' '))
        }
    }

    repositories {
        mavenCentral()
    }
    
    dependencies {
        compile 'org.jetbrains:annotations:16.0.3'
        implementation 'org.antlr:antlr4-runtime:4.7.1'
        antlr 'org.antlr:antlr4:4.7.1'
    }

    build.doLast {
        copy {
            into 'jars'
            from configurations.runtime, jar
        }
    }

    clean.doLast {
        file('jars').deleteDir()
        file('out').deleteDir()
    }

}
