plugins {
    id 'java'
}

allprojects {
    group = 'edu.rpi.aris'
    version = new File('libaris/src/main/resources/edu/rpi/aris/VERSION').text.trim()

    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.8

    jar.doFirst {
        manifest {
            attributes("Implementation-Title": "Aris",
                    "Implementation-Version": version,
                    'Class-Path': project.name + ".jar libs/" + configurations.runtime.collect { it.name }.join(' libs/'))
        }
    }

    repositories {
        mavenCentral()
    }
    
    dependencies {
        compile 'org.jetbrains:annotations:16.0.3'
    }

    build.doLast {
        copy {
            into 'jars/libs'
            from configurations.runtime
        }
        copy {
            into 'jars'
            from jar
            rename (jar.archiveName, project.name + ".jar")
        }
    }

    clean.doLast {
        file('jars').deleteDir()
        file('out').deleteDir()
    }

}
